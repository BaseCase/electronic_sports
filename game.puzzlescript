title Electronic Sport
author BaseCase
homepage caseybrant.com

(realtime_interval 0.5)
throttle_movement

verbose_logging


========
OBJECTS
========

Background
Green

Wall
Brown

Player
White

Player1Start
White
.....
.....
.....
.....
.....

Player2
Black

Player2Start
White
.....
.....
.....
.....
.....

Player1Move1Ready
#00F

Player1Move2Ready
#00E

Player1Move3Ready
#00D

Player1Move4Ready
#00C

Player1Move1Used
Gray

Player1Move2Used
Gray

Player1Move3Used
Gray

Player1Move4Used
Gray

Player2Move1Ready
#F00

Player2Move2Ready
#E00

Player2Move3Ready
#D00

Player2Move4Ready
#C00

Player2Move1Used
Gray

Player2Move2Used
Gray

Player2Move3Used
Gray

Player2Move4Used
Gray

Player1TurnActive
Black Brown
11011
11101
11110
11101
11011

InitPlayer1Turn
Black Brown
11011
10111
01110
10111
11011

Player2TurnActive
Black Brown
11011
10111
01111
10111
11011

InitPlayer2Turn
Black Brown
11011
11101
01110
11101
11011

Ball
White Black
.....
.000.
.010.
.000.
.....

BallStart
White
.....
.....
.....
.....
.....

Player1Goal
White
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

Player2Goal
White
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0


=======
LEGEND
=======

. = Background
# = Wall
@ = Player and Player1Start
& = Player2 and Player2Start
O = Ball and BallStart
1 = Player1Move1Ready
2 = Player1Move2Ready
3 = Player1Move3Ready
4 = Player1Move4Ready
A = Player2Move1Used
B = Player2Move2Used
C = Player2Move3Used
D = Player2Move4Used
Z = Player1TurnActive
[ = Player1Goal
] = Player2Goal

Moves = Player1Move1Ready or Player1Move2Ready or Player1Move3Ready or Player1Move4Ready or Player1Move1Used or Player1Move2Used or Player1Move3Used or Player1Move4Used or Player2Move1Ready or Player2Move2Ready or Player2Move3Ready or Player2Move4Ready or Player2Move1Used or Player2Move2Used or Player2Move3Used or Player2Move4Used

TurnIndicator = Player1TurnActive or Player2TurnActive or InitPlayer1Turn or InitPlayer2Turn

Boundary = Wall or Moves or TurnIndicator

Goal = Player1Goal or Player2Goal

Player1Move1 = Player1Move1Ready or Player1Move1Used
Player1Move2 = Player1Move2Ready or Player1Move2Used
Player1Move3 = Player1Move3Ready or Player1Move3Used
Player1Move4 = Player1Move4Ready or Player1Move4Used

Player2Move1 = Player2Move1Ready or Player2Move1Used
Player2Move2 = Player2Move2Ready or Player2Move2Used
Player2Move3 = Player2Move3Ready or Player2Move3Used
Player2Move4 = Player2Move4Ready or Player2Move4Used


=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background
Goal, BallStart, Player1Start, Player2Start
Player, Player2, Wall, Moves, InitPlayer1Turn, Player1TurnActive, InitPlayer2Turn, Player2TurnActive, Ball


======
RULES
======

(((
*** MOVEMENT
)))

(Player2's turn)
[Moving Player] [Player2] [Player2Move3Used | Player2Move4Ready] -> [Stationary Player] [Moving Player2] [Player2Move3Used | Player2Move4Used]
[Moving Player] [Player2] [Player2Move2Used | Player2Move3Ready] -> [Stationary Player] [Moving Player2] [Player2Move2Used | Player2Move3Used]
[Moving Player] [Player2] [Player2Move1Used | Player2Move2Ready] -> [Stationary Player] [Moving Player2] [Player2Move1Used | Player2Move2Used]
[Moving Player] [Player2] [Player2Move1Ready] -> [Stationary Player] [Moving Player2] [Player2Move1Used]

(Player1's turn)
[Moving Player] [Player1Move3Used | Player1Move4Ready] -> [Moving Player] [Player1Move3Used | Player1Move4Used]
[Moving Player] [Player1Move2Used | Player1Move3Ready] -> [Moving Player] [Player1Move2Used | Player1Move3Used]
[Moving Player] [Player1Move1Used | Player1Move2Ready] -> [Moving Player] [Player1Move1Used | Player1Move2Used]
[Moving Player] [Player1Move1Ready]                    -> [Moving Player] [Player1Move1Used]

(swap control between the two players)
late [Player1TurnActive] [Player1Move4Used] -> [InitPlayer2Turn] [Player1Move4Used]
late [Player2TurnActive] [Player2Move4Used] -> [InitPlayer1Turn] [Player2Move4Used]




(don't use a move by walking into wall)
[> Player  | Wall] -> cancel
[> Player2 | Wall] -> cancel


(((
*** Ball Stuff (TM)
)))
[Player1TurnActive][action Player | Ball | Boundary] -> cancel
[Player1TurnActive][action Player | Ball] -> [Player1TurnActive][Player | > Ball]
[Player1TurnActive][action Player | no Ball] -> cancel

[Player2TurnActive][action Player2 | Ball | Boundary] -> cancel
[Player2TurnActive][action Player2 | Ball] -> [Player2TurnActive][Player2 | > Ball]
[Player2TurnActive][action Player2 | no Ball] -> cancel


late [Player1Goal Ball] [BallStart] [Player] [Player2] [Player1Start] [Player2Start] [TurnIndicator] -> [Player1Goal no Ball] [BallStart Ball] [no Player] [no Player2] [Player1Start Player] [Player2Start Player2] [InitPlayer1Turn] message Player 2 scores!

late [Player2Goal Ball] [BallStart] [Player] [Player2] [Player1Start] [Player2Start] [TurnIndicator] -> [Player2Goal no Ball] [BallStart Ball] [no Player] [no Player2] [Player1Start Player] [Player2Start Player2] [InitPlayer2Turn] message Player 1 scores!


(((
the mechanical part of swapping player remaining moves
happens down here b/c it's init'd in a few different places above and needs to happen last
)))
late [InitPlayer1Turn] [Player1Move1] [Player1Move2] [Player1Move3] [Player1Move4] [Player2Move1] [Player2Move2] [Player2Move3] [Player2Move4] -> [Player1TurnActive] [Player1Move1Ready] [Player1Move2Ready] [Player1Move3Ready] [Player1Move4Ready] [Player2Move1Used] [Player2Move2Used] [Player2Move3Used] [Player2Move4Used]

late [InitPlayer2Turn] [Player1Move1] [Player1Move2] [Player1Move3] [Player1Move4] [Player2Move1] [Player2Move2] [Player2Move3] [Player2Move4] -> [Player2TurnActive] [Player1Move1Used] [Player1Move2Used] [Player1Move3Used] [Player1Move4Used] [Player2Move1Ready] [Player2Move2Ready] [Player2Move3Ready] [Player2Move4Ready]


==============
WINCONDITIONS
==============


=======
LEVELS
=======

#1234#Z#DCBA#
#...........#
[...........]
[@....O....&]
[...........]
#...........#
#############

