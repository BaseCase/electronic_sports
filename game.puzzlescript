title Electronic Sport
author BaseCase
homepage caseybrant.com

(realtime_interval 0.5)
throttle_movement

verbose_logging


========
OBJECTS
========

Background
Green

Wall
Brown

Player
Gray

Player1Start
White
.....
.....
.....
.....
.....

Player2
Black

Player2Start
White
.....
.....
.....
.....
.....

Player1Move1Ready
#00F

Player1Move2Ready
#00E

Player1Move3Ready
#00D

Player1Move4Ready
#00C

Player1Move1Used
Gray

Player1Move2Used
Gray

Player1Move3Used
Gray

Player1Move4Used
Gray

Player2Move1Ready
#F00

Player2Move2Ready
#E00

Player2Move3Ready
#D00

Player2Move4Ready
#C00

Player2Move1Used
Gray

Player2Move2Used
Gray

Player2Move3Used
Gray

Player2Move4Used
Gray

Player1TurnActive
Black Brown
11011
11101
11110
11101
11011

InitPlayer1Turn
Black Brown
11011
10111
01110
10111
11011

Player2TurnActive
Black Brown
11011
10111
01111
10111
11011

InitPlayer2Turn
Black Brown
11011
11101
01110
11101
11011

Ball
White Black
.....
.000.
.010.
.000.
.....

BallStart
White
.....
.....
.....
.....
.....

Player1Goal
White
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

Player2Goal
White
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

BallMovingSE
Red Black
.....
.000.
.000.
.001.
.....

BallMovingNE
Red Black
.....
.001.
.000.
.000.
.....

BallMovingNW
Red Black
.....
.100.
.000.
.000.
.....

BallMovingSW
Red Black
.....
.000.
.000.
.100.
.....

BallMovingUp
Red Black
.....
.010.
.000.
.000.
.....

BallMovingRight
Red Black
.....
.000.
.001.
.000.
.....

BallMovingDown
Red Black
.....
.000.
.000.
.010.
.....

BallMovingLeft
Red Black
.....
.000.
.100.
.000.
.....

TopOfBall
Black
..0..
.....
.....
.....
.....

NWOfBall
Black
0....
.....
.....
.....
.....

NEOfBall
Black
....0
.....
.....
.....
.....

SEOfBall
Black
.....
.....
.....
.....
....0

SWOfBall
Black
.....
.....
.....
.....
0....


=======
LEGEND
=======

. = Background
# = Wall
@ = Player and Player1Start
& = Player2 and Player2Start
O = Ball and BallStart
1 = Player1Move1Ready
2 = Player1Move2Ready
3 = Player1Move3Ready
4 = Player1Move4Ready
A = Player2Move1Used
B = Player2Move2Used
C = Player2Move3Used
D = Player2Move4Used
Z = Player1TurnActive
[ = Player1Goal
] = Player2Goal

Moves = Player1Move1Ready or Player1Move2Ready or Player1Move3Ready or Player1Move4Ready or Player1Move1Used or Player1Move2Used or Player1Move3Used or Player1Move4Used or Player2Move1Ready or Player2Move2Ready or Player2Move3Ready or Player2Move4Ready or Player2Move1Used or Player2Move2Used or Player2Move3Used or Player2Move4Used

TurnIndicator = Player1TurnActive or Player2TurnActive or InitPlayer1Turn or InitPlayer2Turn

Obstacle = Wall or Moves or TurnIndicator or Player or Player2

Goal = Player1Goal or Player2Goal

Player1Move1 = Player1Move1Ready or Player1Move1Used
Player1Move2 = Player1Move2Ready or Player1Move2Used
Player1Move3 = Player1Move3Ready or Player1Move3Used
Player1Move4 = Player1Move4Ready or Player1Move4Used

Player2Move1 = Player2Move1Ready or Player2Move1Used
Player2Move2 = Player2Move2Ready or Player2Move2Used
Player2Move3 = Player2Move3Ready or Player2Move3Used
Player2Move4 = Player2Move4Ready or Player2Move4Used

NextToBall = TopOfBall or NWOfBall or NEOfBall or SEOfBall or SWOfBall

MovingBall = BallMovingSE or BallMovingSW or BallMovingNE or BallMovingNW or BallMovingUp or BallMovingRight or BallMovingDown or BallMovingLeft


=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background
NextToBall, MovingBall
Goal, BallStart, Player1Start, Player2Start
Player, Player2, Wall, Moves, InitPlayer1Turn, Player1TurnActive, InitPlayer2Turn, Player2TurnActive, Ball






======
RULES
======

(((
*** MOVEMENT
)))
(Player2's turn)
[Moving Player] [Player2] [Player2Move3Used | Player2Move4Ready] -> [Stationary Player] [Moving Player2] [Player2Move3Used | Player2Move4Used]
[Moving Player] [Player2] [Player2Move2Used | Player2Move3Ready] -> [Stationary Player] [Moving Player2] [Player2Move2Used | Player2Move3Used]
[Moving Player] [Player2] [Player2Move1Used | Player2Move2Ready] -> [Stationary Player] [Moving Player2] [Player2Move1Used | Player2Move2Used]
[Moving Player] [Player2] [Player2Move1Ready] -> [Stationary Player] [Moving Player2] [Player2Move1Used]

(Player1's turn)
[Moving Player] [Player1Move3Used | Player1Move4Ready] -> [Moving Player] [Player1Move3Used | Player1Move4Used]
[Moving Player] [Player1Move2Used | Player1Move3Ready] -> [Moving Player] [Player1Move2Used | Player1Move3Used]
[Moving Player] [Player1Move1Used | Player1Move2Ready] -> [Moving Player] [Player1Move1Used | Player1Move2Used]
[Moving Player] [Player1Move1Ready]                    -> [Moving Player] [Player1Move1Used]

(swap control between the two players every 4 moves)
late [Player1TurnActive] [Player1Move4Used] -> [InitPlayer2Turn] [Player1Move4Used]
late [Player2TurnActive] [Player2Move4Used] -> [InitPlayer1Turn] [Player2Move4Used]

[> Player  | Wall] -> cancel
[> Player2 | Wall] -> cancel






(((
*** ball proximity
)))
late [NextToBall] -> [ ] (reset and recalculate each turn)

(drop down these invisible markers to we can respond to diagonal kicks)
late up    [Ball | Background] -> [Ball | TopOfBall Background]
late left  [TopOfBall | Background] -> [TopOfBall | NWOfBall Background]
late down  [NWOfBall | | Background] -> [NWOfBall | | SWOfBall Background]
late right [SWOfBall | | Background] -> [SWOfBall | | SEOfBall Background]
late up    [SEOfBall | | Background] -> [SEOfBall | | NEOfBall Background]







(((
*** Kicks and ball movement
)))
(normal kicks)
[Player1TurnActive][action Player | Ball | Obstacle] -> cancel
[Player1TurnActive][action Player | Ball] -> [Player1TurnActive][Player | > Ball]
([Player1TurnActive][action Player | no Ball] -> cancel)

[Player2TurnActive][action Player2 | Ball | Obstacle] -> cancel
[Player2TurnActive][action Player2 | Ball] -> [Player2TurnActive][Player2 | > Ball]
([Player2TurnActive][action Player2 | no Ball] -> cancel)

(dribbling -- run into the ball to fancy footwork it behind you)
[> Player  | Ball] -> [Ball | Player]
[> Player2 | Ball] -> [Ball | Player2]

(diagonal kicks)
[action Player NWOfBall] [Ball] [no Obstacle SEOfBall] -> [action Player NWOfBall] [BallMovingSE] [ ]
[action Player SWOfBall] [Ball] [no Obstacle NEOfBall] -> [action Player SWOfBall] [BallMovingNE] [ ]
[action Player SEOfBall] [Ball] [no Obstacle NWOfBall] -> [action Player SEOfBall] [BallMovingNW] [ ]
[action Player NEOfBall] [Ball] [no Obstacle SWOfBall] -> [action Player NEOfBall] [BallMovingSW] [ ]

(ball in motion)
(late right [BallMovingSE | Player2] -> [ | BallMovingDown Player2])
late right [BallMovingSE | ] -> [ | BallMovingDown]
late left  [BallMovingSW | ] -> [ | BallMovingDown]
late right [BallMovingNE | ] -> [ | BallMovingUp]
late left  [BallMovingNW | ] -> [ | BallMovingUp]

late down  [BallMovingDown | no Obstacle] -> [ | Ball]
late up    [BallMovingUp   | no Obstacle] -> [ | Ball]








(((
*** GOOOOOOOOLLLLs
)))
late [Player1Goal Ball] [BallStart] [Player] [Player2] [Player1Start] [Player2Start] [TurnIndicator] -> [Player1Goal no Ball] [BallStart Ball] [no Player] [no Player2] [Player1Start Player] [Player2Start Player2] [InitPlayer1Turn] message Player 2 scores!

late [Player2Goal Ball] [BallStart] [Player] [Player2] [Player1Start] [Player2Start] [TurnIndicator] -> [Player2Goal no Ball] [BallStart Ball] [no Player] [no Player2] [Player1Start Player] [Player2Start Player2] [InitPlayer2Turn] message Player 1 scores!









(((
*** the mechanical part of swapping player remaining moves
*** happens down here b/c it's init'd in a few different places above and needs to happen last
)))
late [InitPlayer2Turn] -> [InitPlayer1Turn] (delete this line to activate player 2)
late [InitPlayer1Turn] [Player1Move1] [Player1Move2] [Player1Move3] [Player1Move4] [Player2Move1] [Player2Move2] [Player2Move3] [Player2Move4] -> [Player1TurnActive] [Player1Move1Ready] [Player1Move2Ready] [Player1Move3Ready] [Player1Move4Ready] [Player2Move1Used] [Player2Move2Used] [Player2Move3Used] [Player2Move4Used]

(((( uncomment to activate player 2's moves
late [InitPlayer2Turn] [Player1Move1] [Player1Move2] [Player1Move3] [Player1Move4] [Player2Move1] [Player2Move2] [Player2Move3] [Player2Move4] -> [Player2TurnActive] [Player1Move1Used] [Player1Move2Used] [Player1Move3Used] [Player1Move4Used] [Player2Move1Ready] [Player2Move2Ready] [Player2Move3Ready] [Player2Move4Ready]
))))






==============
WINCONDITIONS
==============


=======
LEVELS
=======

#1234#Z#DCBA#
#...........#
[...........]
[@....O....&]
[...........]
#...........#
#############

